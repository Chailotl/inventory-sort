plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.6.3"
	id "dev.kikugie.stonecutter"
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url 'https://maven.wispforest.io' }
	maven { url "https://maven.axoga.to/releases" }
}

loom {
	accessWidenerPath = file("src/main/resources/inventory_sort.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
	include "io.wispforest:owo-sentinel:${project.owo_version}"

	modImplementation "com.chailotl:sushi_bar:${project.sushi_version}"
}

loom {
	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and just create separate worlds
	}
}

processResources {
	def map = [
		"name": project.mod_name,
		"version": project.version,
		"minecraft_version": project.minecraft_version
	]

	inputs.properties(map)
	filesMatching("fabric.mod.json") { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

if (project.gradle.getProperties().get("modrinth_token") != null) {
	publishMods {
		File changelogFile = new File("CHANGELOG.md")

		file = remapJar.archiveFile
		changelog = changelogFile.exists() ? changelogFile.text : ""
		type = STABLE
		displayName = "${project.mod_name} ${project.mod_version}"
		modLoaders.add("fabric")

		modrinth {
			accessToken = providers.gradleProperty("modrinth_token").get()
			projectId = "s9XXQTOb"
			minecraftVersionRange {
				start = project.minecraft_version
				end = project.minecraft_version_max
			}
			requires("fabric-api")
			requires("owo-lib")
			requires("sushi-bar")
			optional("modmenu")
		}
	}
}